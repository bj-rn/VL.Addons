// source:
// https://github.com/vvvv/vvvv-sdk/tree/develop/vvvv45/addonpack/lib/nodes/modules/TextureFX/Filter/AutoColor

[Category("Filter.ColorManipulation")]
[Summary("Automatic color adjustment")]

shader AutoColor_Pass1_Internal_TextureFX : FilterBase
{
	[Default(1.0)]
    compose ComputeFloat Power;

    [Default(0.0)]
    compose ComputeFloat AutoGamma;

    [Default(0.0)]
    compose ComputeFloat AutoMax;

    float4 Filter(float4 tex0col)
    {
        float2 uv = streams.TexCoord;
        float4 m = Texture1.SampleLevel(LinearSampler, uv, 99);

        float cmax = pow(m.x, 1.0 / Power.Compute());
        float cmin = m.z;
        float cavg = m.y;

        cmax = lerp(1, cmax, AutoMax.Compute());
        cavg = (cavg - cmin) / max(cmax - cmin, 0.00000001);

        float4 c = (Texture0.SampleLevel(LinearSampler, uv, 0) - cmin) / max(cmax - cmin, 0.00000001);
        c.rgb = sign(c.rgb) * pow(abs(c.rgb), pow(0.5 / cavg, AutoGamma.Compute()));
        c.a = Texture0.SampleLevel(LinearSampler, uv, 0).a;

        return c;
    }
};