// source:
// https://github.com/mrvux/dx11-vvvv-girlpower/blob/master/nodes/texture11/Filter/DropShadow.tfx

[Category("Filter.Rendering")]
[Summary("")]
[WantsMips("Texture0")]
shader DropShadow_Internal_TextureFX : FilterBase
{
    [Default(0.5f)]
    compose ComputeFloat Blur;

    [Default(0.05f)]
    compose ComputeFloat Offset;

    [Default(0.9f)]
    compose ComputeFloat Angle;

    [Default(0.0f, 0.0f, 0.0f, 1.0f)]
    compose ComputeColor Color;  

    [Default(1.0f)]
    compose ComputeFloat Alpha;

    [Default(0.0f)]
    compose ComputeFloat Extension;

    float4 Filter(float4 tex0col)
    {
        float2 uv = streams.TexCoord;
        float2 R = ViewSize;
    
        float4 c0 = Texture0.SampleLevel(Sampler0, uv, 0);

        float lod = Blur.Compute() * log2(max(R.x, R.y));
        
        float4 col = 0;
        
        float2 sx = uv + sqrt(2) * Offset.Compute() * sin((Angle.Compute() + float2(0, -0.25)) * acos(-1.0) * 2.0);
        float2 off = .5 * Texture0TexelSize * pow(2, lod) * saturate(lod);
        
        col += Texture0.SampleLevel(Sampler0, sx+float2(0,0)*off, lod);
        col += Texture0.SampleLevel(Sampler0, sx+float2(1,1)*off, lod);
        col += Texture0.SampleLevel(Sampler0, sx+float2(1,-1)*off, lod);
        col += Texture0.SampleLevel(Sampler0, sx+float2(-1,-1)*off,lod);
        col += Texture0.SampleLevel(Sampler0, sx+float2(-1,1)*off, lod);
        
        off *= 1.86;
        
        col += Texture0.SampleLevel(Sampler0, sx+float2(0,1)*off, lod);
        col += Texture0.SampleLevel(Sampler0, sx+float2(0,-1)*off, lod);
        col += Texture0.SampleLevel(Sampler0, sx+float2(-1,0)*off, lod);
        col += Texture0.SampleLevel(Sampler0, sx+float2(1,0)*off, lod);
        
        col /= 9;
        
        col = Color.Compute() * float4(1, 1, 1, saturate(pow(2, Extension.Compute())*col.a) * Alpha.Compute());

        col.rgb = lerp(col.rgb, c0.rgb, c0.a);
        col.a = max(col.a, c0.a);
        
        return col;
    }
};