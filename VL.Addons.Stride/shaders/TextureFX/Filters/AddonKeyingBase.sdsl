shader AddonKeyingBase : FilterBase
{
    // the variables to tweak the keying
    [Default(0.005f, 0.1f)]
    compose ComputeFloat2 MaskRange;

    bool Invert;

    // method to calculate the mask with soft edge
    float CalcMask(float dist)
    {
        float2 maskRange = MaskRange.Compute();
        if (dist < maskRange.x)
            return 0.0;
        else if (dist < maskRange.y)
            return (dist - maskRange.x)/(maskRange.y - maskRange.x);
        else
            return 1.0;
    }

    // the distance function
    abstract float CalcKeyDist(float4 col);

    stage override float4 Shading()
    {
        float4 col = InTex0();

        // get distance to key
        float dist = CalcKeyDist(col);

        // get mask
        float mask = CalcMask(dist);

        return Invert ? 1 - mask : mask;
    }
};