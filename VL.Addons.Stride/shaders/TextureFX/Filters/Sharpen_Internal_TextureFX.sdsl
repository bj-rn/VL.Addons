// source:
// https://github.com/vvvv/vvvv-sdk/blob/develop/vvvv45/addonpack/lib/nodes/modules/TextureFX/Filter/Sharpen/Sharpen.fx

[Category("Filter.Enhancement")]
[Summary("")]
[WantsMips("Texture0")]

shader Sharpen_Internal_TextureFX : FilterBase
{
    [Default(0.1f)]
    compose ComputeFloat Amount;

    [Default(0.0f)]
    compose ComputeFloat Radius;

    [Default(0.0f)]
    compose ComputeFloat Saturation;

    [Default(0.0f)]
    compose ComputeFloat Gamma;
  
    float4 Filter(float4 tex0col)
    {
        float2 uv = streams.TexCoord;

        float4 col = Texture0.SampleLevel(Sampler0, uv, 0);
    
        float maxl = log2(max(ViewSize.x, ViewSize.y));// + 0.5;
        float4 sh = 0;
   
        float radius = Radius.Compute();

        sh.rgb += (Texture0.SampleLevel(Sampler0, uv, 0 + maxl * saturate(radius)) 
                 - Texture0.SampleLevel(Sampler0, uv, 1 + maxl * saturate(radius))) 
                 * 128 * Amount.Compute() / pow(2, radius/max(ViewSize.x, ViewSize.y) * pow(2, maxl * radius));
    
        sh.rgb = lerp(dot(sh.rgb, 1.0) / 3.0, sh.rgb, Saturation.Compute());
    
        sh.rgb = sign(sh.rgb) * pow(abs(sh.rgb) * 5.0, pow(2, Gamma.Compute() * 2.0)) / 5.0;
        
        sh /= 1 + col;

        col.rgb *= pow(2, sh);
    
        return col;
    }
};